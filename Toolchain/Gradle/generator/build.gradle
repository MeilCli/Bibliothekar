import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.21' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.7' apply false
}

def reportMerge = tasks.register("detekt", ReportMergeTask) {
    output = project.layout.buildDirectory.file("reports/detekt/merge.xml")
}

subprojects {
    afterEvaluate {
        plugins.withType(DetektPlugin) {
            tasks.withType(Detekt) { detektTask ->
                finalizedBy(reportMerge)

                reportMerge.configure { mergeTask ->
                    mergeTask.input.from(detektTask.xmlReportFile)
                    mergeTask.dependsOn(detektTask)
                }
            }
        }
    }
}

tasks.register("check") {
    dependsOn ':dependencies:check'
}

tasks.register("clean") {
    dependsOn ':dependencies:clean'
}

tasks.register("assemble") {
    dependsOn ':dependencies:assemble'
}

tasks.register("testAll") {
    dependsOn ':dependencies:test'
}
