import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.20' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.24' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.7' apply false
    id 'com.android.library' version '7.3.1' apply false
}

def reportMerge = tasks.register("detekt", ReportMergeTask) {
    output = project.layout.buildDirectory.file("reports/detekt/merge.xml")
}

subprojects {
    afterEvaluate {
        plugins.withType(DetektPlugin) {
            tasks.withType(Detekt) { detektTask ->
                finalizedBy(reportMerge)

                reportMerge.configure { mergeTask ->
                    mergeTask.input.from(detektTask.xmlReportFile)
                    mergeTask.dependsOn(detektTask)
                }
            }
        }
    }
}

tasks.register("check") {
    subprojects.forEach {
        dependsOn "${it.path}:check"
    }
}

tasks.register("clean") {
    subprojects.forEach {
        dependsOn "${it.path}:clean"
    }
}

tasks.register("assemble") {
    subprojects.forEach {
        dependsOn "${it.path}:assemble"
    }
}

tasks.register("testAll") {
    subprojects.forEach {
        dependsOn "${it.path}:test"
    }
}
